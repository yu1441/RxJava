apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.yujing.rxjava"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'SERVER_PORT', server_port
            buildConfigField 'String', 'SERVER_IP', server_ip
            buildConfigField 'String', 'SERVER_URL', server_url
            manifestPlaceholders = ["app_name": "@string/app_name_debug"]
            applicationIdSuffix ".debug"//包名后缀
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField 'String', 'SERVER_PORT', server_port
            buildConfigField 'String', 'SERVER_IP', server_ip
            buildConfigField 'String', 'SERVER_URL', server_url
            manifestPlaceholders = ["app_name": "@string/app_name"]
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
    }

    //打包
    android.applicationVariants.all { variant ->
        //获取渠道名称
        //def appName = variant.productFlavors[0].name
        def appName = "RxJava"
        def fileName//获取当前时间的"YYYY-MM-dd"格式。
        def createDate = new Date().format("YYYY-MM-dd", TimeZone.getTimeZone("GMT+08:00"))
        def createTime = new Date().format("HH-mm-ss", TimeZone.getTimeZone("GMT+08:00"))
        variant.outputs.each {
            //判断是release还是debug版本
            def buildType = variant.buildType.name
            if (buildType == "release") {
                variant.getPackageApplicationProvider().get().outputDirectory = new File(project.projectDir.absolutePath + "/apk")
                fileName = "${appName}_v${defaultConfig.versionName}_${versionCode}_${createDate}_${createTime}_${buildType}.apk"
                it.outputFileName = fileName
            }
            if (buildType == "debug") {
                variant.getPackageApplicationProvider().get().outputDirectory = new File(project.projectDir.absolutePath + "/build/apk")
                fileName = "${appName}_v${defaultConfig.versionName}_${versionCode}_${buildType}.apk"
                it.outputFileName = fileName
            }
        }
    }
    //签名
    signingConfigs {
        debug {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
        release {
            storeFile file("doc/yujingtest.jks")
            storePassword "123456"
            keyAlias "test"
            keyPassword "123456"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.github.yutils.utils3:net:3.3.6'
    implementation 'com.github.yutils.utils3:utils:3.3.6'
    implementation 'com.github.yutils.utils3:yurl:3.3.6'
    implementation 'com.github.yutils.utils3:ycrash:3.3.6'

    //gson  <!-- https://mvnrepository.com/artifact/com.google.code.gson/gson -->
    implementation 'com.google.code.gson:gson:2.8.5'
    //对应的是com.android.support:recyclerview-v7:28.0.2
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    //-------------------------------------------------------------------------------------------------------

    //rxjava2，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.13'
    //rxandroid，https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxandroid
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'
    // rxbinding，https://mvnrepository.com/artifact/com.jakewharton.rxbinding3/rxbinding
    implementation group: 'com.jakewharton.rxbinding3', name: 'rxbinding', version: '3.0.0'

    //自动解绑，https://mvnrepository.com/artifact/com.trello.rxlifecycle3/rxlifecycle-components
    implementation group: 'com.trello.rxlifecycle3', name: 'rxlifecycle-components', version: '3.1.0'
    //-------------------------------------------------------------------------------------------------------

    //retrofit，https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.6.2'
    //支持Gson解析，https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.6.2'
    //衔接 Retrofit & RxJava，注意使用RxJava2的版本，https://mvnrepository.com/artifact/com.jakewharton.retrofit/retrofit2-rxjava2-adapter
    implementation group: 'com.jakewharton.retrofit', name: 'retrofit2-rxjava2-adapter', version: '1.0.0'

    //-------------------------------------------------------------------------------------------------------

    // https://mvnrepository.com/artifact/com.blankj/rxbus
    implementation group: 'com.blankj', name: 'rxbus', version: '1.6'

    //okhttp，https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    //https://mvnrepository.com/artifact/org.conscrypt/conscrypt-openjdk-uber
    implementation group: 'org.conscrypt', name: 'conscrypt-openjdk-uber', version: '2.2.1'
    //okhttp日志打印，// https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    //adapter-rxjava，https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava
    //implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava', version: '2.6.2'
    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-scalars
    //implementation group: 'com.squareup.retrofit2', name: 'converter-scalars', version: '2.6.2'
}
